buildscript {
    ext {
        pactVersion = '3.5.17'
    }
}

plugins {
    id "application"
    id "java"
    id "au.com.dius.pact" version "3.5.17"
}

version = "1.5"
mainClassName = "no.dervis.java.app.Main"
sourceCompatibility = 1.8

dependencies {
    compile "org.apache.httpcomponents:fluent-hc:4.5"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.5"
    testCompile "au.com.dius:pact-jvm-consumer-java8_2.12:$pactVersion"
    testCompile "au.com.dius:pact-jvm-consumer-junit5_2.12:$pactVersion"
}

pact {
    publish {
        pactBrokerUrl = "https://norwegianlga.pact.dius.com.au"
        pactBrokerUsername = System.getenv('PACTBROKER_USERNAME')
        pactBrokerPassword = System.getenv('PACTBROKER_PASSWORD')
        tags = [System.getenv('ENV_TAG')]
    }
}

apply from: "${rootDir}/common.gradle"

def pactFolderString = "$buildDir" + File.separator + "pacts"
test.systemProperties["pact.rootDir"] = pactFolderString

// Publish Pacts to Pact Broker
pactPublish {
    doFirst {
        if (!file(pactFolderString).exists() ||
                file("$buildDir/pacts").listFiles(new FilenameFilter() {
                    @Override
                    boolean accept(File dir, String name) {
                        def jsonfile = name.toLowerCase().endsWith(".json")
                        if (jsonfile) println "Found a Pact file: $name"
                        return jsonfile
                    }
                }).length == 0) {
            enabled = false
        }
    }
}

build.finalizedBy(pactPublish)